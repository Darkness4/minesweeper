import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'java'
}

group 'marc.nguyen'
version '1.0'

repositories {
    mavenCentral()
    jcenter()
}

test {
    useJUnitPlatform()
}

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

apply from: rootProject.file('gradle/install-git-hooks.gradle')

dependencies {
    implementation 'org.jetbrains:annotations:19.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'marc.nguyen.minesweeper.server.Server'
}

task serverJar(type: Jar) {
    manifest {
        attributes(
                'Main-Class': 'marc.nguyen.minesweeper.server.Server'
        )
    }
    archiveClassifier.set("server")
    from sourceSets.main.output
}

task serverShadowJar(type: ShadowJar) {
    manifest {
        inheritFrom project.tasks.serverJar.manifest
    }
    archiveClassifier.set("all-server")
    from sourceSets.main.output
    configurations = [ configurations.runtime ]
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
}

task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'marc.nguyen.minesweeper.client.Client'
}

task clientJar(type: Jar) {
    manifest {
        attributes(
                'Main-Class': 'marc.nguyen.minesweeper.client.Client'
        )
    }
    archiveClassifier.set("client")
    from sourceSets.main.output
}

task clientShadowJar(type: ShadowJar) {
    manifest {
        inheritFrom project.tasks.clientJar.manifest
    }
    archiveClassifier.set("all-client")
    from sourceSets.main.output
    configurations = [ configurations.runtime ]
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
}